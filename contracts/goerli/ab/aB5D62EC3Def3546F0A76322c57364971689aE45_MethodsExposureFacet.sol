// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

// import {IERC20Upgradeable} from "@openzeppelin-contracts-upgradeable/interfaces/IERC20Upgradeable.sol";

import {LibDiamond} from "../libraries/LibDiamond.sol";
import {IDiamondCut} from "../interfaces/IDiamondCut.sol";
import {IDiamondLoupe} from "../interfaces/IDiamondLoupe.sol";
import {FacetCut, Facet} from "../types/diamond/Facet.sol";

contract MethodsExposureFacet is IDiamondCut, IDiamondLoupe {
  // ==================== IDiamondLoupe & IDiamondCut ==================== //

  /// @notice Add/replace/remove any number of functions and optionally execute
  ///         a function with delegatecall
  /// @param _diamondCut Contains the facet addresses and function selectors
  /// @param _init The address of the contract or facet to execute _calldata
  /// @param _calldata A function call, including function selector and arguments
  ///                  _calldata is executed with delegatecall on _init
  function diamondCut(
    FacetCut[] calldata _diamondCut,
    address _init,
    bytes calldata _calldata
  ) external {}

  /// These functions are expected to be called frequently by tools.

  /// @notice Gets all facets and their selectors.
  /// @return facets_ Facet
  function facets() external view override returns (Facet[] memory facets_) {
    facets_ = new Facet[](0);
  }

  /// @notice Gets all the function selectors provided by a facet.
  /// @param _facet The facet address.
  /// @return facetFunctionSelectors_
  function facetFunctionSelectors(
    address _facet
  ) external view override returns (bytes4[] memory facetFunctionSelectors_) {
    facetFunctionSelectors_ = new bytes4[](0);
  }

  /// @notice Get all the facet addresses used by a diamond.
  /// @return facetAddresses_
  function facetAddresses() external view override returns (address[] memory facetAddresses_) {
    facetAddresses_ = new address[](0);
  }

  /// @notice Gets the facet that supports the given selector.
  /// @dev If facet is not found return address(0).
  /// @param _functionSelector The function selector.
  /// @return facetAddress_ The facet address.
  function facetAddress(
    bytes4 _functionSelector
  ) external view override returns (address facetAddress_) {
    return address(0);
  }

  // ==================== ERC20 ==================== //

  function name() public view virtual returns (string memory) {
    return "";
  }

  function symbol() public view virtual returns (string memory) {
    return "";
  }

  function totalSupply() external view returns (uint256) {
    return 0;
  }

  function balanceOf(address account) external view returns (uint256) {
    return 0;
  }

  function transfer(address to, uint256 amount) external returns (bool) {
    return false;
  }

  function allowance(address owner, address spender) external view returns (uint256) {
    return 0;
  }

  function approve(address spender, uint256 amount) external returns (bool) {
    return false;
  }

  function transferFrom(address from, address to, uint256 amount) external returns (bool) {
    return false;
  }

  function setLiquidityWallet(address _liquidityWallet) external {}

  function setLiquidityFee(uint256 _liquidityBuyFee) external {}

  function setRewardFee(uint256 _rewardBuyFee) external {}

  function setIsLpPool(address _pairAddress, bool _isLp) external {}

  function setNumTokensToSwap(uint256 _amount) external {}

  function setDefaultRouter(address _router) external {}

  function setSwapRouter(address _router, bool _isRouter) external {}

  function setRewardToken(
    address token,
    address router,
    address[] calldata path,
    bool useV3,
    bytes calldata pathV3
  ) external {}

  function setManualClaim(bool manualClaim) external {}

  function setMaxTokenPerWallet(uint256 newMaxTokenPerWallet) external {}

  function setProcessRewards() external {}

  function setProcessFeeRole(address account) external {}

  function withdraw(uint256 amount) external {}

  // ==================== Views ==================== //

  function implementation() external view returns (address) {
    return address(0);
  }

  function liquidityWallet() external view returns (address) {
    return address(0);
  }

  function isExcludedFromFee(address account) external view returns (bool) {
    return false;
  }

  function isExcludedMaxWallet(address account) external view returns (bool) {
    return false;
  }

  function isExcludedFromRewards(address account) external view returns (bool) {
    return false;
  }

  function isSwapRouter(address routerAddress) external view returns (bool) {
    return false;
  }

  function isLpPool(address pairAddress) external view returns (bool) {
    return false;
  }

  function isProcessingRewards() external view returns (bool) {
    return false;
  }

  function maxTokenPerWallet() external view returns (uint256) {
    return 0;
  }

  // ==================== DividendPayingToken ==================== //

  function dividendOf(address _owner) public view returns (uint256 dividends) {
    return 0;
  }

  function withdrawnDividendOf(address _owner) public view returns (uint256 dividends) {
    return 0;
  }

  function accumulativeDividendOf(address _owner) public view returns (uint256 accumulated) {
    return 0;
  }

  function withdrawableDividendOf(address _owner) public view returns (uint256 withdrawable) {
    return 0;
  }

  function dividendBalanceOf(address account) public view returns (uint256) {
    return 0;
  }

  // ==================== HamachiFacet ==================== //

  function claimRewards() external {}

  function getRewardToken() external view returns (address token, address router, bool isV3) {
    return (address(0), address(0), false);
  }

  function totalRewardSupply() public view returns (uint256) {
    return 0;
  }

  /// @return numHolders The number of reward tracking token holders
  function getRewardHolders() external view returns (uint256 numHolders) {
    return 0;
  }

  /// Gets reward account information by address
  function getRewardAccount(
    address _account
  )
    public
    view
    returns (
      address account,
      int256 index,
      int256 numInQueue,
      uint256 rewardBalance,
      uint256 withdrawableRewards,
      uint256 totalRewards,
      bool manualClaim
    )
  {
    return (address(0), 0, 0, 0, 0, 0, false);
  }

  function buyFees() public view returns (uint256, uint256, uint256) {
    return (0, 0, 0);
  }

  function sellFees() external view returns (uint256, uint256, uint256) {
    return (0, 0, 0);
  }

  function totalBuyFees() public view returns (uint256) {
    return 0;
  }

  function totalSellFees() public view returns (uint256) {
    return 0;
  }

  function numTokensToSwap() external view returns (uint256) {
    return 0;
  }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {FacetCut} from "../types/diamond/Facet.sol";

/******************************************************************************\
* Author: Nick Mudge <[email protected]> (https://twitter.com/mudgen)
* EIP-2535 Diamonds: https://eips.ethereum.org/EIPS/eip-2535
/******************************************************************************/

interface IDiamondCut {
  /// @notice Add/replace/remove any number of functions and optionally execute
  ///         a function with delegatecall
  /// @param _diamondCut Contains the facet addresses and function selectors
  /// @param _init The address of the contract or facet to execute _calldata
  /// @param _calldata A function call, including function selector and arguments
  ///                  _calldata is executed with delegatecall on _init
  function diamondCut(
    FacetCut[] calldata _diamondCut,
    address _init,
    bytes calldata _calldata
  ) external;

  event DiamondCut(FacetCut[] _diamondCut, address _init, bytes _calldata);
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {Facet} from "../types/diamond/Facet.sol";

/******************************************************************************\
* Author: Nick Mudge <[email protected]> (https://twitter.com/mudgen)
* EIP-2535 Diamonds: https://eips.ethereum.org/EIPS/eip-2535
/******************************************************************************/

// A loupe is a small magnifying glass used to look at diamonds.
// These functions look at diamonds
interface IDiamondLoupe {
  /// These functions are expected to be called frequently
  /// by tools.

  /// @notice Gets all facet addresses and their four byte function selectors.
  /// @return facets_ Facet
  function facets() external view returns (Facet[] memory facets_);

  /// @notice Gets all the function selectors supported by a specific facet.
  /// @param _facet The facet address.
  /// @return facetFunctionSelectors_
  function facetFunctionSelectors(
    address _facet
  ) external view returns (bytes4[] memory facetFunctionSelectors_);

  /// @notice Get all the facet addresses used by a diamond.
  /// @return facetAddresses_
  function facetAddresses() external view returns (address[] memory facetAddresses_);

  /// @notice Gets the facet that supports the given selector.
  /// @dev If facet is not found return address(0).
  /// @param _functionSelector The function selector.
  /// @return facetAddress_ The facet address.
  function facetAddress(bytes4 _functionSelector) external view returns (address facetAddress_);
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

/******************************************************************************\
* Author: Nick Mudge <[email protected]> (https://twitter.com/mudgen)
* EIP-2535 Diamonds: https://eips.ethereum.org/EIPS/eip-2535
/******************************************************************************/
import {IDiamondCut} from "../interfaces/IDiamondCut.sol";
import {FacetCut, FacetCutAction} from "../types/diamond/Facet.sol";
import {DiamondStorage} from "../types/diamond/DiamondStorage.sol";

library LibDiamond {
  error InValidFacetCutAction();
  error NoSelectorsInFacet();
  error NoZeroAddress();
  error SelectorExists(bytes4 selector);
  error SameSelectorReplacement(bytes4 selector);
  error MustBeZeroAddress();
  error NoCode();
  error NonExistentSelector(bytes4 selector);
  error ImmutableFunction(bytes4 selector);
  error NonEmptyCalldata();
  error EmptyCalldata();
  error InitCallFailed();

  bytes32 internal constant DIAMOND_STORAGE_POSITION =
    keccak256("diamond.standard.diamond.storage");

  function DS() internal pure returns (DiamondStorage storage ds) {
    bytes32 position = DIAMOND_STORAGE_POSITION;
    assembly {
      ds.slot := position
    }
  }

  event DiamondCut(FacetCut[] _diamondCut, address _init, bytes _calldata);

  // Internal function version of diamondCut
  function diamondCut(
    FacetCut[] memory _diamondCut,
    address _init,
    bytes memory _calldata
  ) internal {
    for (uint256 facetIndex; facetIndex < _diamondCut.length; facetIndex++) {
      FacetCutAction action = _diamondCut[facetIndex].action;
      if (action == FacetCutAction.Add) {
        addFunctions(
          _diamondCut[facetIndex].facetAddress,
          _diamondCut[facetIndex].functionSelectors
        );
      } else if (action == FacetCutAction.Replace) {
        replaceFunctions(
          _diamondCut[facetIndex].facetAddress,
          _diamondCut[facetIndex].functionSelectors
        );
      } else if (action == FacetCutAction.Remove) {
        removeFunctions(
          _diamondCut[facetIndex].facetAddress,
          _diamondCut[facetIndex].functionSelectors
        );
      } else {
        revert InValidFacetCutAction();
      }
    }
    emit DiamondCut(_diamondCut, _init, _calldata);
    initializeDiamondCut(_init, _calldata);
  }

  function addFunctions(address _facetAddress, bytes4[] memory _functionSelectors) internal {
    if (_functionSelectors.length <= 0) revert NoSelectorsInFacet();
    DiamondStorage storage ds = DS();
    if (_facetAddress == address(0)) revert NoZeroAddress();
    uint96 selectorPosition = uint96(
      ds.facetFunctionSelectors[_facetAddress].functionSelectors.length
    );
    // add new facet address if it does not exist
    if (selectorPosition == 0) {
      addFacet(ds, _facetAddress);
    }
    for (uint256 selectorIndex; selectorIndex < _functionSelectors.length; selectorIndex++) {
      bytes4 selector = _functionSelectors[selectorIndex];
      address oldFacetAddress = ds.selectorToFacetAndPosition[selector].facetAddress;
      if (oldFacetAddress != address(0)) revert SelectorExists(selector);
      addFunction(ds, selector, selectorPosition, _facetAddress);
      selectorPosition++;
    }
  }

  function replaceFunctions(address _facetAddress, bytes4[] memory _functionSelectors) internal {
    if (_functionSelectors.length <= 0) revert NoSelectorsInFacet();
    DiamondStorage storage ds = DS();
    if (_facetAddress == address(0)) revert NoZeroAddress();
    uint96 selectorPosition = uint96(
      ds.facetFunctionSelectors[_facetAddress].functionSelectors.length
    );
    // add new facet address if it does not exist
    if (selectorPosition == 0) {
      addFacet(ds, _facetAddress);
    }
    for (uint256 selectorIndex; selectorIndex < _functionSelectors.length; selectorIndex++) {
      bytes4 selector = _functionSelectors[selectorIndex];
      address oldFacetAddress = ds.selectorToFacetAndPosition[selector].facetAddress;
      if (oldFacetAddress == _facetAddress) revert SameSelectorReplacement(selector);
      removeFunction(ds, oldFacetAddress, selector);
      addFunction(ds, selector, selectorPosition, _facetAddress);
      selectorPosition++;
    }
  }

  function removeFunctions(address _facetAddress, bytes4[] memory _functionSelectors) internal {
    if (_functionSelectors.length <= 0) revert NoSelectorsInFacet();
    DiamondStorage storage ds = DS();
    // if function does not exist then do nothing and return
    if (_facetAddress != address(0)) revert MustBeZeroAddress();
    for (uint256 selectorIndex; selectorIndex < _functionSelectors.length; selectorIndex++) {
      bytes4 selector = _functionSelectors[selectorIndex];
      address oldFacetAddress = ds.selectorToFacetAndPosition[selector].facetAddress;
      removeFunction(ds, oldFacetAddress, selector);
    }
  }

  function addFacet(DiamondStorage storage ds, address _facetAddress) internal {
    enforceHasContractCode(_facetAddress);
    ds.facetFunctionSelectors[_facetAddress].facetAddressPosition = ds.facetAddresses.length;
    ds.facetAddresses.push(_facetAddress);
  }

  function addFunction(
    DiamondStorage storage ds,
    bytes4 _selector,
    uint96 _selectorPosition,
    address _facetAddress
  ) internal {
    ds.selectorToFacetAndPosition[_selector].functionSelectorPosition = _selectorPosition;
    ds.facetFunctionSelectors[_facetAddress].functionSelectors.push(_selector);
    ds.selectorToFacetAndPosition[_selector].facetAddress = _facetAddress;
  }

  function removeFunction(
    DiamondStorage storage ds,
    address _facetAddress,
    bytes4 _selector
  ) internal {
    if (_facetAddress == address(0)) revert NonExistentSelector(_selector);
    // an immutable function is a function defined directly in a diamond
    if (_facetAddress == address(this)) revert ImmutableFunction(_selector);
    // replace selector with last selector, then delete last selector
    uint256 selectorPosition = ds.selectorToFacetAndPosition[_selector].functionSelectorPosition;
    uint256 lastSelectorPosition = ds
      .facetFunctionSelectors[_facetAddress]
      .functionSelectors
      .length - 1;
    // if not the same then replace _selector with lastSelector
    if (selectorPosition != lastSelectorPosition) {
      bytes4 lastSelector = ds.facetFunctionSelectors[_facetAddress].functionSelectors[
        lastSelectorPosition
      ];
      ds.facetFunctionSelectors[_facetAddress].functionSelectors[selectorPosition] = lastSelector;
      ds.selectorToFacetAndPosition[lastSelector].functionSelectorPosition = uint96(
        selectorPosition
      );
    }
    // delete the last selector
    ds.facetFunctionSelectors[_facetAddress].functionSelectors.pop();
    delete ds.selectorToFacetAndPosition[_selector];

    // if no more selectors for facet address then delete the facet address
    if (lastSelectorPosition == 0) {
      // replace facet address with last facet address and delete last facet address
      uint256 lastFacetAddressPosition = ds.facetAddresses.length - 1;
      uint256 facetAddressPosition = ds.facetFunctionSelectors[_facetAddress].facetAddressPosition;
      if (facetAddressPosition != lastFacetAddressPosition) {
        address lastFacetAddress = ds.facetAddresses[lastFacetAddressPosition];
        ds.facetAddresses[facetAddressPosition] = lastFacetAddress;
        ds.facetFunctionSelectors[lastFacetAddress].facetAddressPosition = facetAddressPosition;
      }
      ds.facetAddresses.pop();
      delete ds.facetFunctionSelectors[_facetAddress].facetAddressPosition;
    }
  }

  function initializeDiamondCut(address _init, bytes memory _calldata) internal {
    if (_init == address(0)) {
      if (_calldata.length > 0) revert NonEmptyCalldata();
    } else {
      if (_calldata.length == 0) revert EmptyCalldata();
      if (_init != address(this)) {
        enforceHasContractCode(_init);
      }
      (bool success, bytes memory error) = _init.delegatecall(_calldata);
      if (!success) {
        if (error.length > 0) {
          // bubble up the error
          revert(string(error));
        } else {
          revert InitCallFailed();
        }
      }
    }
  }

  function enforceHasContractCode(address _contract) internal view {
    uint256 contractSize;
    assembly {
      contractSize := extcodesize(_contract)
    }
    if (contractSize <= 0) revert NoCode();
  }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {FacetAddressAndPosition, FacetFunctionSelectors} from "./Facet.sol";

struct DiamondStorage {
  // maps function selector to the facet address and
  // the position of the selector in the facetFunctionSelectors.selectors array
  mapping(bytes4 => FacetAddressAndPosition) selectorToFacetAndPosition;
  // maps facet addresses to function selectors
  mapping(address => FacetFunctionSelectors) facetFunctionSelectors;
  // facet addresses
  address[] facetAddresses;
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

struct Facet {
  address facetAddress;
  bytes4[] functionSelectors;
}

struct FacetCut {
  address facetAddress;
  FacetCutAction action;
  bytes4[] functionSelectors;
}

enum FacetCutAction {
  // Add=0, Replace=1, Remove=2
  Add,
  Replace,
  Remove
}

struct FacetAddressAndPosition {
  address facetAddress;
  uint96 functionSelectorPosition; // position in facetFunctionSelectors.functionSelectors array
}

struct FacetFunctionSelectors {
  bytes4[] functionSelectors;
  uint256 facetAddressPosition; // position of facetAddress in facetAddresses array
}